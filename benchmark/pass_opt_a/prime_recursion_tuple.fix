module Main;

is_prime : I64 -> Array Bool;
is_prime = |n| (
    let arr = Array::fill(n, true);
    let arr = arr.set(0, false);
    let arr = arr.set(1, false);
    is_prime_loop((n, 2, arr))
);

is_prime_loop : (I64, I64, Array Bool) -> Array Bool;
is_prime_loop = |(n, i, arr)| (
    if i*i > n { arr };
    let next_arr = (
        if !arr.@(i) { arr };
        is_prime_inner_loop((n, i+i, i, arr))
    );
    is_prime_loop((n, i+1, next_arr))
);

is_prime_inner_loop : (I64, I64, I64, Array Bool) -> Array Bool;
is_prime_inner_loop = |(n, q, p, arr)| (
    if n-1 < q { arr };
    is_prime_inner_loop((n, q+p, p, arr.set(q, false)))
);

// Count the appearance of a value in an array.
count : [a: Eq] a -> Array a -> I64;
count = |elem, arr| count_loop((0, 0, elem, arr));

count_loop : [a: Eq] (I64, I64, a, Array a) -> I64;
count_loop = |(i, sum, elem, arr)| (
    if arr.get_size == i { sum };
    let sum = sum + (if arr.@(i) == elem {1} else {0});
    count_loop((i+1, sum, elem, arr))
);

main : IO ();
main = println $ is_prime(10000000).count(true).to_string; // 664579
